plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
    id "com.cinnober.gradle.semver-git" version "2.4.0"
}

group 'org.jskele.libs.dao'

ext {
    springBootVersion = '2.1.5.RELEASE'
}

apply plugin: "java-library"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
    }
}

ext {
    jUnitVersion = '5.2.0'
}

dependencies {
    annotationProcessor(
            "org.springframework:spring-context-indexer",
            'org.projectlombok:lombok',
    )

    compileOnly(
            'org.projectlombok:lombok',
            // if application disables classpath scanning, then reflections is used to find `@Dao` classes
            'org.reflections:reflections:0.9.11',
    )

    api(
            // Exposing spring-context via api dependencies configuration, because
            // `annotationProcessor 'org.springframework:spring-context-indexer'`
            // should be used by projects using this project (to detect `@Dao` classes)
            // and spring-context is needed for compiling classes annotated with `@Dao` (as it uses `@Indexed`)
            'org.springframework:spring-context',
    )

    implementation(
            'com.google.guava:guava',
            'org.springframework:spring-jdbc',
            'org.springframework.boot:spring-boot-autoconfigure',
            'commons-beanutils:commons-beanutils:1.9.3',
            'org.apache.commons:commons-lang3',

            'org.jskele:jskele-values:2.0',

            'com.mitchellbosecke:pebble:2.4.0',
    )

    testAnnotationProcessor(
            "org.springframework:spring-context-indexer",
            "org.projectlombok:lombok",
    )

    testCompileOnly(
            'org.projectlombok:lombok',
    )

    testImplementation(
            'org.springframework.boot:spring-boot-starter-jdbc',
            'ru.yandex.qatools.embed:postgresql-embedded:2.6',
            "org.junit.jupiter:junit-jupiter-params:$jUnitVersion",
    )
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
    }

    testRuntimeOnly(
            'org.postgresql:postgresql',
            'org.springframework.boot:spring-boot-starter-logging',
            "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion",
    )

    sourceSets {
        test {
            resources {
                srcDir sourceSets.test.java // sql failid on java DAO klasside kÃµrval
            }
        }
    }
}

test {
    useJUnitPlatform()
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    pkg {
        repo = 'jskele'
        name = 'lib-dao-package'
        version {
            name = '1.0'
            desc = '1.0'
            released = new Date()
        }
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

publishToMavenLocal.doLast {
    println("The published artifact can be consumed by other local maven projects from local maven repo using ${project.group}:${project.name}:${project.version}")
}
